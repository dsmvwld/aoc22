      ⍝ Day 10: Cathode-Ray Tube

      ⍝ part 1

      load←{(⎕UCS 10)(≠⊆⊢)⊃⎕NGET AoCPath,⍵}
      
      noop←1 0
      addx←2

      ∇s←reminus s;i
       i←s⍳'-'
       :If i≤⍴s
           s[i]←'¯'
       :EndIf
      ∇

      anew←{X←1 ⋄ T←⍬}
      step←{(n d)←⍵ ⋄ T,←n⍴X ⋄ X+←d}
      strength←{⍵×T[⍵]}
      probe←¯20+40×⍳6

      anew ⋄ step¨⍎¨reminus¨load'10.example.txt'
      +/strength probe
13140

      ⍝ part 2

      '.#'[1+6 40⍴1≥|T-240⍴¯1+⍳40]
##..##..##..##..##..##..##..##..##..##..
###...###...###...###...###...###...###.
####....####....####....####....####....
#####.....#####.....#####.....#####.....
######......######......######......####
#######.......#######.......#######.....

